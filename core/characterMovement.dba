Rem: Player Character Movement Logic

GLOBAL movePlayerCharacterX#, movePlayerCharacterY#, movePlayerCharacterz#, isPlayerCharacterMoving, playerCharacterMoveSpeed#
GLOBAL idleStartFrame#, idleEndFrame#, walkStartFrame#, walkEndFrame#, isWalkAnimationPlaying, walkingfx#
GLOBAL isChasingEnemy, isAttackingEnemy, attackStartFrame#, attackEndFrame#, attackRadius#

FUNCTION Init_Player_Movement()
	movePlayerCharacterX#     = OBJECT POSITION X(1)
	movePlayerCharacterY#     = OBJECT POSITION Y(1)
	movePlayerCharacterZ#     = OBJECT POSITION Z(1)
	isPlayerCharacterMoving   = 0
	isChasingEnemy            = 0
	playerCharacterMoveSpeed# = 0.025
	idleStartFrame#           = 0
	idleEndFrame#             = 24
	walkStartFrame#           = idleEndFrame# + 1
	walkEndFrame#             = 50
	attackStartFrame#         = walkEndFrame# + 1
	attackEndFrame#           = TOTAL OBJECT FRAMES(1) - 1
	attackRadius#             = 2.0
	isWalkAnimationPlaying    = 0
	isChasingEnemy            = 0
	isAttackingEnemy          = 0
ENDFUNCTION

FUNCTION Click_To_Move()
	Rem detect left click 1
	IF MOUSECLICK() = 1
		
		Rem get nearest ray vector
		PICK SCREEN MOUSEX(), MOUSEY(), 1
		nearVectX# = GET PICK VECTOR X()
		nearVectY# = GET PICK VECTOR Y()
		nearVectZ# = GET PICK VECTOR Z()
		
		Rem get farthest ray vector
		PICK SCREEN MOUSEX(), MOUSEY(), 1000
		farVectX# = GET PICK VECTOR X()
		farVectY# = GET PICK VECTOR Y()
		farVectZ# = GET PICK VECTOR Z()
		
		camX# = CAMERA POSITION X(0)
		camY# = CAMERA POSITION Y(0)
		camZ# = CAMERA POSITION Z(0)
		
		nearX# = camX# + nearVectX#
		nearY# = camY# + nearVectY#
		nearZ# = camZ# + nearVectZ#
		
		farX# = camX# + farVectX#
		farY# = camY# + farVectY#
		farZ# = camZ# + farVectZ#
		
		Rem intersect ray with ground plane at y = 0
		deltaY# = farY# - nearY#
		IF deltaY# <> 0
			t# = (0 - nearY#) / deltaY#
			IF t# >= 0
				movePlayerCharacterX#   = nearX# + (farX# - nearX#) * t#
				movePlayerCharacterZ#   = nearZ# + (farZ# - nearZ#) * t#
				isPlayerCharacterMoving = 1
			ENDIF
		ENDIF
		
		Rem make model turn to face intersected ray
		POINT OBJECT 1, movePlayerCharacterX#, movePlayerCharacterY#, movePlayerCharacterZ#
		
		Rem play walk animation
		IF isWalkAnimationPlaying = 0
			LOOP OBJECT 1, 30, walkEndFrame#
			SET OBJECT SPEED 1, 31.5
			isWalkAnimationPlaying = 1
			LOOP SOUND 1
		ENDIF
		
	ENDIF
ENDFUNCTION

FUNCTION Update_Player_Movement()

	IF isPlayerCharacterMoving = 1
		
		currentCharX# = OBJECT POSITION X(1)
		currentCharZ# = OBJECT POSITION Z(1)
	
		IF isChasingEnemy = 1
			
			enemyX# = OBJECT POSITION X(enemyId#)
			enemyZ# = OBJECT POSITION Z(enemyId#)
			
			deltaX# = enemyX# - currentCharX#
			deltaZ# = enemyZ# - currentCharZ#
			distanceToTarget# = SQRT((deltaX# * deltaX#) + (deltaZ# * deltaZ#))
			
			IF distanceToTarget# > attackRadius#
			
				normalizedX# = deltaX# / distanceToTarget#
 				normalizedZ# = deltaZ# / distanceToTarget#
 				
 				newCharX# = currentCharX# + normalizedX# * playerCharacterMoveSpeed#
				newCharZ# = currentCharZ# + normalizedZ# * playerCharacterMoveSpeed#
				
				POSITION OBJECT 1, newCharX#, OBJECT POSITION Y(1), newCharZ#
				
			ELSE
				
				isPlayerCharacterMoving = 0
				isChasingEnemy          = 0
				isAttackingEnemy        = 1
				
				Rem start attacking
				LOOP OBJECT 1, attackStartFrame#, attackEndFrame#
				SET OBJECT SPEED 1, 35
				STOP SOUND 1
				POINT OBJECT 1, enemyX#, 0, enemyZ#
				
			ENDIF
			
		ELSE

			Rem normal click to move
			deltaX# = movePlayerCharacterX# - currentCharX#
			deltaZ# = movePlayerCharacterZ# - currentCharZ#
			
			distanceToTarget# = SQRT((deltaX# * deltaX#) + (deltaZ# * deltaZ#))
			
			IF distanceToTarget# > 1.0
			
				Rem normalize direction
				normalizedX# = deltaX# / distanceToTarget#
				normalizedZ# = deltaZ# / distanceToTarget#
			
				Rem move towards target
				newCharX# = currentCharX# + normalizedX# * playerCharacterMoveSpeed#
				newCharZ# = currentCharZ# + normalizedZ# * playerCharacterMoveSpeed#
				
				POSITION OBJECT 1, newCharX#, OBJECT POSITION Y(1), newCharZ#
				
			ELSE
				isPlayerCharacterMoving = 0
				
				Rem player stops at destination
				IF isWalkAnimationPlaying = 1
					isWalkAnimationPlaying = 0			
					LOOP OBJECT 1, idleStartFrame#, idleEndFrame#
					SET OBJECT SPEED 1, 10
					STOP SOUND 1
				ENDIF Rem player stops
				
			ENDIF Rem distance to target greater than 1.0
			
		ENDIF Rem isChasingEnemy = 1
			
	ENDIF Rem is player moving = 1
		
ENDFUNCTION

Rem check if floor or enemy was clicked
FUNCTION Check_Mouse_Click()
	IF MOUSECLICK() = 1
	
		mx# = MOUSEX()
		my# = MOUSEY()
		
		clickedObj# = PICK OBJECT(mx#, my#, 3, 5)
		
		IF clickedObj# = enemyId#
			isPlayerCharacterMoving = 1
			isChasingEnemy          = 1
		ELSE
			Rem clicked the ground
			isChasingEnemy          = 0
			isAttackingEnemy        = 0
			isPlayerCharacterMoving = 1
			STOP OBJECT 1
			LOOP OBJECT 1, 30, walkEndFrame#
			SET OBJECT SPEED 1, 31.5
		ENDIF
	ENDIF
ENDFUNCTION