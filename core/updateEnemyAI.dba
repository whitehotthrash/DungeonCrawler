Rem Controls enemy sight radius and movement

GLOBAL enemySightRadius#, enemyMovementSpeed#, enemyStopRadius#, isEnemyIdle, isEnemyChasing, isEnemyMoving, enemyWalkSfx

FUNCTION Init_Enemy_AI()
	enemySightRadius#   = 5.0
	enemyMovementSpeed# = 0.02
	enemyStopRadius#    = 2.0
	isEnemyChasing      = 0
	isEnemyMoving       = 0
	isEnemyIdle         = 1
	enemyWalkSfx        = 0
ENDFUNCTION


FUNCTION Enemy_AI_Update()

	playerX# = OBJECT POSITION X(1)
	playerZ# = OBJECT POSITION Z(1)
	
	enemyX#  = OBJECT POSITION X(enemyId#)
	enemyZ#  = OBJECT POSITION Z(enemyId#)
	
	deltaX#  = playerX# - enemyX#
	deltaZ#  = playerZ# - enemyZ#
	distanceToPlayer# = SQRT((deltaX# * deltaX#) + (deltaZ# * deltaZ#))
	
	Rem move towards player
	IF distanceToPlayer# < enemySightRadius#
	
		isEnemyIdle    = 0
		isEnemyChasing = 1
		
		POINT OBJECT enemyId#, playerX#, OBJECT POSITION Y(1), playerZ#
		
		IF distanceToPlayer# > enemyStopRadius#
			
			IF enemyWalkSfx = 0
				LOOP SOUND 2
				enemyWalkSfx = 1
			ENDIF
		
			IF isEnemyMoving = 0
				isEnemyMoving = 1
				LOOP OBJECT enemyId#, 30, 50
				SET OBJECT SPEED enemyId#, 31.5
			ENDIF
		
			Rem normalise
			deltaX# = deltaX# / distanceToPlayer#
			deltaZ# = deltaZ# / distanceToPlayer#
			
			Rem move towards player
			enemyX# = enemyX# + deltaX# * enemyMovementSpeed#
			enemyZ# = enemyZ# + deltaZ# * enemyMovementSpeed#
			
			POSITION OBJECT enemyId#, enemyX#, OBJECT POSITION Y(enemyId#), enemyZ#
			
		ELSE
			Rem if sfx is playing when not moving
			IF enemyWalkSfx = 1
				STOP SOUND 2
				enemyWalkSfx = 0
			ENDIF
			
			IF isEnemyMoving = 1
				isEnemyMoving = 0
				LOOP OBJECT enemyId#, 0, 25
			ENDIF
			
			isEnemyChasing = 0
			isEnemyIdle    = 1

		ENDIF
		
	ELSE
		Rem if sfx is playing when not targeting player
		IF enemyWalkSfx = 1
			STOP SOUND 2
			enemyWalkSfx = 0
		ENDIF
		
		IF isEnemyMoving = 1
			isEnemyMoving = 0
			LOOP OBJECT enemyId#, 0, 25
		ENDIF
		
		isEnemyChasing = 0
		isEnemyIdle    = 0
		
	ENDIF

ENDFUNCTION
